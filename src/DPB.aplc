:Class DPB
    ⍝ Determinate Progress Bar in Dyalog APL.
    
    :Field Private defaults ← ('style' 0)('total' 100)('length' 20)('decimals' 0)('empty' ' ')
    :Field Private total ← ¯1
    :Field Private ticks ← ¯1
    :Field Private length ← ¯1
    :Field Private states ← ⍬
    :Field Private empty ← ''
    :Field Private intervals ← ⍬
    :Field Private formatter ← ¯1 ¯1
    :Field Private totalWidth ← 0
    
    ∇ InitDefault
        ⍝ Default constructor that uses all the defaults.
        :Access Public
        :Implements Constructor
        Init defaults
    ∇
    
    ∇ Init options
        ⍝ Customisable constructor.
        ;keys ;style
        ;V ;I ;F ;P
        :Access Public
        :Implements Constructor
        
        ⍝ Add the default values and create a function to read the Values of the options.
        options ,← defaults
        keys ← ¯1⎕C⊣/↑options
        V ← {⊃⌽⊃options[keys⍳⊂⍵]}
        
        ⍝ Total ticks of the progress bar.
        total ← 1⌈V'total'
        ⍝ Ticks so far.
        ticks ← 0

        ⍝ Length of the interior of the progress bar.        
        length ← 1⌈V'length'
        
        ⍝ Character to use in the empty spaces of the progress bar.                    
        empty ← V'empty'                                           

        F ← ('[',']',⍨⊢)    ⍝ Frame the states with [ ... ]
        ⍝ Build a progression with successively more chars `⍵` and less `empty`s
        P ← {(⍵@(⍸0⍪∘.≥⍨⍳length))empty⍴⍨1 0+length}
        ⍝ Consecutive printable states.
        :Select ⊃style ← V'style'
        :CaseList 0 1 2 3 4 5
            states ← ↓F P'#>█@X⎕'[style]
        :Case 10     
            states ← ↓F P⊃⌽style
        :CaseList 20 21 22
            ⍝ Build the state strings incrementally as a progression of the given characters.
            I ← {i ← ⍺,⍨(≢⍺)⌿¯1↑⍵ ⋄ i⍪⍨⍵,empty}⍣length⍨
            states ← ↓F I⍪(20|style)⊃'▏▎▍▌▋▊▉█' '▎▌▊█' '▌█'
        :Else
            states ← ↓F P'#'
        :EndSelect
        ⍝ Prepare to use ⍸ to find the state that should be printed.
        intervals ← (¯1+≢states)÷⍨⍳≢states
        
        ⍝ Formatting for the percentage of progress completion.
        formatter ← (⊢,⍨3+⊢+×)0⌈V'decimals'
        
        totalWidth ← (3++/formatter)+≢⊃states
    ∇
    
    ∇ Print ;p
        ⍝ Print the current bar state.
        :Access Public
                      
        Clear
        ⍞← (⊃states[intervals⍸p]),⍨'[','%]',⍨formatter⍕100×p←0⌈1⌊ticks÷total
    ∇
    
    ∇ ts ← Tick t
        ⍝ Increment the current bar progress by `t` ticks.
        ⍝ Returns the total progress of the bar so far.
        :Access Public
        ts ← ticks ← total⌊ticks + t      
    ∇       
    
    ∇ ts ← TickAndPrint t
        ⍝ Increment the current bar progress by `t` ticks and print the new state.
        ⍝ Returns the total progress of the bar so far.
        :Access Public
        ts ← Tick t
        Print
    ∇
    
    ∇ ts ← Set t
        ⍝ Set the current tick progress to `t`.
        :Access Public
        ts ← ticks ← 0⌈total⌊t
    ∇
    
    ∇ ts ← SetP p
        ⍝ Set the current progress to the given percentage (in the [0, 1] interval).
        ⍝ Returns the current progress in terms of ticks.
        :Access Public
        ts ← ticks ← total×0⌈1⌊p
    ∇
    
    ∇ ts ← Query
        ⍝ Query the current progress in terms of ticks.
        :Access Public
        ts ← ticks
    ∇
    
    ∇ p ← QueryP
        ⍝ Query the current percentual progress.
        :Access Public
        p ← ticks÷total
    ∇
    
    ∇ Clear
        ⍝ Uses backspaces and non-breaking spaces to clear the current line.
        ⍞← totalWidth/⎕UCS 8 160 8
    ∇
:EndClass
