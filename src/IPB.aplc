:Class IPB
    ⍝ Customisable indeterminate progress bars. 
                        
    :Field Private defaults ← ('start' 0)('style' 0)('length' 20)('dt' 0.2)('framed' 1)('prefix' '')('postfix' '')
    :Field Private tn ← ⍬
    :Field Private l ← 0
    :Field Private dt ← 0
    :Field Private states ← ''
    :Field Private prefix ← ''
    :Field Private postfix ← ''
    
    ∇ InitDefault
        :Access Public
        :Implements Constructor
        Init defaults
    ∇

    ∇ Init options
        ;keys ;start ;style ;length
        ;V ;On
        :Access Public
        :Implements Constructor
         
        options ,← defaults
        keys ← ¯1⎕C⊣/↑options
        V ← {⊃⌽⊃options[keys⍳⊂⍵]}
        On ← ((,1)1∨.≡⊢)
        
        ⍝ Save important values.
        l ← 1⌈V'length'
        dt ← 0.001⌈V'dt'          
        prefix ← V'prefix'
        postfix ← V'postfix'

        ⍝ Set the PB style.
        :Select V'style'
        :Case 1
            :If l=1
                states ← ,(,'.')
            :Else
                states ← ↓' .'[⍉2⊥⍣¯1⊢¯1↓(¯1∘↓,⌽)¯1↓∊(⍪,{0,⍨2+/⍵})2*⌽⍳l]
            :EndIf
        :Case 2
            states ← ⌽'█▉▊▋▌▍▎▏'
            states ← {l↑states[⍵|⍨≢states],⍨(⌊⍵÷≢states)/⊃⌽states}¨(¯1∘↓,⌽)⍳l×≢states
        :Else
            states ← (⍳∘≢l∘⍴⍤⌽¨⊂) '_/¯\'
        :EndSelect
        
        :If On V'framed'
            states ← ('[',⊢,']'⍨)¨states
        :EndIf
        
        :If On V'start'
            _← Start
        :EndIf
    ∇
    
    ∇ r ← Start
        :Access Public
        
        r ← tn≡⍬
        :If r        
            tn ← {Loop}&⍬
        :EndIf
    ∇         
    
    ∇ Loop
        :Repeat
            Clear
            states ⌽⍨← 1
            ⍞← prefix,postfix,⍨⊃states
            ⎕DL dt
        :EndRepeat
    ∇
    
    ∇ Clear
        ⍞← (2+l+prefix+⍥≢postfix)/⎕UCS 8 160 8
    ∇
    
    ∇ r ← Stop
        :Access Public
        
        r ← tn≢⍬
        :If r
            ⎕TKILL tn
            tn ← ⍬
            Clear   
        :EndIf
    ∇
:EndClass
